apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def configPropertiesFile = rootProject.file("config.properties")
def configProperties = new Properties()
configProperties.load(new FileInputStream(configPropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.guerra.enrico.sera"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }
    buildTypes {
        debug {
            buildConfigField "String", "ApiBaseUri", configProperties['ApiBaseUri']
            buildConfigField "String", "OAUTH2_CLIENT_ID",  configProperties['OAUTH2_CLIENT_ID']
        }
        release {
            buildConfigField "String", "ApiBaseUri",  configProperties['ApiBaseUri']
            buildConfigField "String", "OAUTH2_CLIENT_ID",  configProperties['OAUTH2_CLIENT_ID']

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }

    configurations.all {
        resolutionStrategy.force "com.android.tools.build.jetifier:jetifier-core:1.0.0-beta02"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

kapt{
    generateStubs = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    // Architecture Component

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycleVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Dagger Android
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"

    // RxLint
    implementation "nl.littlerobots.rxlint:rxlint:$rxlintVersion"

    // Work Manager
    implementation "android.arch.work:work-runtime:$workManagerVersion"
    implementation "android.arch.work:work-rxjava2:$workManagerVersion"

    // Shimmer
    implementation "com.facebook.shimmer:shimmer:$shimmerVersion"

    // Google Signin
    implementation "com.google.android.gms:play-services-auth:$googlePlayServicesVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    testImplementation "androidx.test:core:$testCoreVersion"
    testImplementation "androidx.arch.core:core-testing:$testArchCoreVersion"

    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspresso"
}
